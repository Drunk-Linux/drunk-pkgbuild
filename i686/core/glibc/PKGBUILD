pkgname=glibc
pkgver=2.33
pkgrel=1
arch=('i686')
source=(https://ftp.gnu.org/gnu/glibc/glibc-$pkgver.tar.xz
	locale.gen.txt
	locale-gen
	lib32-glibc.conf
	sdt.h sdt-config.h
	bz27343.patch
	0001-nptl_db-Support-different-libpthread-ld.so-load-orde.patch
	0002-nptl-Check-for-compatible-GDB-in-nptl-tst-pthread-gd.patch
	0003-nptl-Do-not-build-nptl-tst-pthread-gdb-attach-as-PIE.patch
	3-7-misc-syslog-Fix-indentation-and-style.diff)

prepare() {
	mkdir -p glibc-build
	mkdir -p lib32-glibc-build
	[[ -d glibc-$pkgver ]] && ln -s glibc-$pkgver glibc 
	cd glibc

	# commit c3479fb7939898ec22c655c383454d6e8b982a67
	patch -p1 -i "$srcdir"/bz27343.patch

	# nptl_db: Support different libpthread/ld.so load orders (bug 27744)
	patch -p1 -i "$srcdir"/0001-nptl_db-Support-different-libpthread-ld.so-load-orde.patch

	# nptl: Check for compatible GDB in nptl/tst-pthread-gdb-attach
	patch -p1 -i "$srcdir"/0002-nptl-Check-for-compatible-GDB-in-nptl-tst-pthread-gd.patch

	# nptl: Do not build nptl/tst-pthread-gdb-attach as PIE
	patch -p1 -i "$srcdir"/0003-nptl-Do-not-build-nptl-tst-pthread-gdb-attach-as-PIE.patch
}

build() {
	cd "$srcdir/glibc-build"

#	export CFLAGS="$CFLAGS -mno-tls-direct-seg-refs"

	$srcdir/glibc/configure			\
	--target=i686				\
	--host=i686-pc-linux-gnu		\
	--prefix=/usr				\
	--with-headers=/usr/include		\
	--enable-add-ons			\
	--enable-kernel=4.4			\
	--disable-werror

	make
}

package() {
	install -dm755 "$pkgdir/etc"
	touch "$pkgdir/etc/ld.so.conf"

	make -C glibc-build install_root="$pkgdir" install
	rm -f "$pkgdir"/etc/ld.so.{cache,conf}

	cd glibc

	install -dm755 "$pkgdir"/usr/lib/{locale,systemd/system,tmpfiles.d}
	install -m644 nscd/nscd.conf "$pkgdir/etc/nscd.conf"
	install -m644 nscd/nscd.service "$pkgdir/usr/lib/systemd/system"
	install -m644 nscd/nscd.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/nscd.conf"
	install -dm755 "$pkgdir/var/db/nscd"

	install -m644 posix/gai.conf "$pkgdir"/etc/gai.conf
	install -m755 "$srcdir/locale-gen" "$pkgdir/usr/bin"

	# Create /etc/locale.gen
	install -m644 "$srcdir/locale.gen.txt" "$pkgdir/etc/locale.gen"
	sed -e '1,3d' -e 's|/| |g' -e 's|\\| |g' -e 's|^|#|g' \
		"$srcdir/glibc/localedata/SUPPORTED" >> "$pkgdir/etc/locale.gen"

	# Provide tracing probes to libstdc++ for exceptions, possibly for other
	# libraries too. Useful for gdb's catch command.
	install -Dm644 "$srcdir/sdt.h" "$pkgdir/usr/include/sys/sdt.h"
	install -Dm644 "$srcdir/sdt-config.h" "$pkgdir/usr/include/sys/sdt-config.h"
}
