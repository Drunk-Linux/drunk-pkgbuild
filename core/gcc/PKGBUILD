# Description: The GNU compiler collection, which includes the C and C++ compilers
# URL:         
# Maintainer:  Emmett1, emmett1 dot 2miligrams at gmail dot com
# Depends on:  

pkgname=gcc
pkgver=11.1.0
pkgrel=1
arch=('x86_64')
source=(https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver/$pkgname-$pkgver.tar.xz)

build() {
	cd $pkgname-$pkgver

	sed -e '/m64=/s/lib64/lib/' \
	        -i.orig gcc/config/i386/t-linux64

	mkdir -v build
	cd       build

	SED=sed                               \
	../configure                          \
	             --prefix=/usr            \
	             --enable-languages=c,c++ \
	             --disable-bootstrap      \
	             --with-system-zlib       \
	             --enable-multilib
	make
}

package() {
	cd $pkgname-$pkgver
	cd build
	make -j1 DESTDIR=$pkgdir install

	rm -rf $pkgdir/usr/lib/gcc/$(gcc -dumpmachine)/$pkgver/include-fixed/bits/

	chown -v -R root:root \
	    $pkgdir/usr/lib/gcc/*linux-gnu/$pkgver/include{,-fixed}

	mkdir -p $pkgdir/lib
	ln -sv ../usr/bin/cpp $pkgdir/lib

	ln -sv gcc $pkgdir/usr/bin/cc

	install -v -dm755 $pkgdir/usr/lib/bfd-plugins
	ln -sfv ../../libexec/gcc/$(gcc -dumpmachine)/$pkgver/liblto_plugin.so \
	        $pkgdir/usr/lib/bfd-plugins/

	mkdir -pv $pkgdir/usr/share/gdb/auto-load/usr/lib
	mv -v $pkgdir/usr/lib/*gdb.py $pkgdir/usr/share/gdb/auto-load/usr/lib
}
