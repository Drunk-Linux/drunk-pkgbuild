pkgname=polkit
pkgver=0.120
pkgrel=4
arch=('x86_64')
depends=(glib2 icu systemd duktape)
makedepends=(meson gtk-doc gobject-introspection git)
commitid=a6bedfd09b7bba753de7a107dc471da0db801858
source=(git+https://gitlab.freedesktop.org/polkit/polkit.git#commit=$commitid)

build() {
	meson build $pkgname	\
	--prefix=/usr		\
	--libdir=/usr/lib	\
	-D js_engine=duktape	\
	-D tests=false		\
	-D man=false		\
	-D session_tracking=libsystemd-login

	meson compile -C build
}

package() {
	meson install -C build --destdir $pkgdir

	install -d -o root -g 102 -m 750 $pkgdir/{etc,usr/share}/polkit-1/rules.d
	mv $pkgdir/{etc,usr/share}/polkit-1/rules.d/50-default.rules

	install -Dm644 /dev/stdin $pkgdir/usr/lib/sysusers.d/$pkgname.conf <<END
u polkitd 102 "PolicyKit daemon"
m polkitd proc
END
}
