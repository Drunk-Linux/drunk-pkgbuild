pkgname=fontconfig
pkgver=2.14.0
pkgrel=1
arch=('x86_64')
depends=(glibc libpng expat zstd bzip2 readline freetype2)
source=(https://freedesktop.org/software/$pkgname/release/$pkgname-$pkgver.tar.gz
	40-fontconfig-config.script
	40-fontconfig-config.hook
	fontconfig.hook)
backup=(etc/fonts/fonts.conf)
install=fontconfig.install

prepare() {
	mkdir -p $pkgname-$pkgver/build
}

build() {
        cd $pkgname-$pkgver/build

	meson			\
	--prefix=/usr		\
	--sysconfdir=/etc	\
	--localstatedir=/var	\
	-D doc-txt=disabled	\
	-D doc-pdf=disabled	\
	-D doc-html=disabled	\
	..
	
	meson compile
}

package() {
	cd $pkgname-$pkgver/build

	export FONTCONFIG_PATH=/etc/fonts
	meson install --destdir "$pkgdir"

	cd $srcdir

	# Handle conf.d using the hook to avoid overwriting the symlinks on upgrade
	mkdir -p "$pkgdir/usr/share/fontconfig/conf.default"
	for _f in "$pkgdir"/etc/fonts/conf.d/*.conf; do
		ln -sfr "$pkgdir"/usr/share/fontconfig/conf.{avail,default}/"${_f##*/}"
		rm -f "$_f"
	done

	install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook
	install -D 40-fontconfig-config.script \
	"$pkgdir/usr/share/libalpm/scripts/40-fontconfig-config"
}
