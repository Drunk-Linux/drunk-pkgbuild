pkgname=tdesktop
pkgver=3.7.5
pkgrel=1
pkgdesc='Official Telegram Desktop client'
arch=('x86_64')
url="https://github.com/telegramdesktop/tdesktop"
commitid=36b8d03b1d455f9cca8d32ae5b33c419a104d6fa
depends=('ffmpeg' 'hicolor-icon-theme' 'lz4' 'minizip' 'openal'
    'qt6' 'glibmm' 'rnnoise' 'pipewire' 'libxtst' 'libxrandr'
    'abseil-cpp' 'tg_owt' 'webkit2gtk')
makedepends=('cmake' 'git' 'ninja' 'python3' 'meson'
    'extra-cmake-modules' 'wayland-protocols' 'plasma-wayland-protocols')
source=("git+https://github.com/telegramdesktop/tdesktop.git")

prepare() {
    cd $pkgname

    git checkout $commitid

    # Fix .git after our clone and sync/update submodules
    git remote remove origin
    git remote add origin $url

    git submodule update --init --recursive

    # Reset all submodules to original changes
    git submodule foreach --recursive git reset --hard
}

build() {
    cd $pkgname

    #
    # API ID/HASH is only and only allowed to be used by DrunkOS developers
    # Killing or using our trust for something else will mean hidden api keys in the future!!!

    export CXXFLAGS+=" -Wp,-U_GLIBCXX_ASSERTIONS"
    cmake \
    -B build \
    -G Ninja \
    -D CMAKE_INSTALL_PREFIX="/usr" \
    -D DESKTOP_APP_DISABLE_WAYLAND_INTEGRATION=on \
    -D DESKTOP_APP_QT6=TRUE \
    -D DESKTOP_APP_USE_PACKAGED_FONTS=true \
    -D CMAKE_BUILD_TYPE=Release \
    -D TDESKTOP_API_ID=12851169 \
    -D TDESKTOP_API_HASH=bc117a073c479e6d4f19c141cc9ac75f

    ninja -C build
}

package() {
    cd $pkgname

    DESTDIR=$pkgdir ninja -C build install
}
