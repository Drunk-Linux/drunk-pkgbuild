pkgname=pipewire
_commit=64cf5e80e6240284e6b757907b900507fe56f1b5  # tags/0.3.50
pkgver=0.3.50
pkgrel=1
arch=(x86_64)
makedepends=(git meson doxygen python3 valgrind pulseaudio
 alsa-lib libusb gst-plugins-base dbus sdl2 ncurses
 libcanberra libsndfile bluez webrtc-audio-processing libx11 libxfixes)
source=("git+https://gitlab.freedesktop.org/pipewire/pipewire.git#commit=$_commit")
install=pipewire.install

prepare() {
	cd pipewire

	# remove export of LD_LIBRARY_PATH for pw-jack as it would add /usr/lib
	sed -i '/LD_LIBRARY_PATH/d' pipewire-jack/src/pw-jack.in
}

build() {
	local meson_options=(
	--prefix=/usr
	-D docs=disabled
	-D jack=disabled
	-D jack-devel=true
	-D libjack-path=/usr/lib
	-D libcamera=disabled
	-D udevrulesdir=/usr/lib/udev/rules.d
	-D roc=disabled
	-D session-managers=[]
	)

	meson pipewire build "${meson_options[@]}"
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"

	rm -rf $pkgdir/usr/{include/jack,lib/libjack*,lib/pkgconfig/jack.pc}
}
