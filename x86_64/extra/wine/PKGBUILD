pkgname=wine
pkgver=6.23
wver=6.x
pkgrel=1
arch=('x86_64')
depends=(fontconfig libxml2 libxcursor libxrandr libxdamage libxi gettext freetype2 libsm gcc)
makedepends=(libpng gnutls libxinerama libxcomposite libxmu libxxf86vm
		mesa libxslt)
source=(https://dl.winehq.org/wine/source/$wver/$pkgname-$pkgver.tar.xz)

prepare() {
	# Allow ccache to work
	mv $pkgname-$pkgver $pkgname

	mkdir -p $pkgname-64-build

	sed 's|OpenCL/opencl.h|CL/opencl.h|g' -i $pkgname/configure*

	# Fix openldap 2.5+ detection
	sed 's/-lldap_r/-lldap/' -i $pkgname/configure

	# Get rid of old build dirs
	rm -rf $pkgname-64-build
}

build() {
	mkdir -p $pkgname-64-build
	cd "$srcdir/$pkgname-64-build"

	../$pkgname/configure		\
	--prefix=/usr			\
	--libdir=/usr/lib		\
	--with-x			\
	--enable-win64

	make
}

package() {
	cd "$srcdir/$pkgname-64-build"

	make				\
	prefix=$pkgdir/usr		\
	bindir=$pkgdir/usr/bin		\
	sbindir=$pkgdir/usr/sbin	\
	libdir=$pkgdir/usr/lib		\
	dlldir=$pkgdir/usr/lib/wine	\
	install
}
