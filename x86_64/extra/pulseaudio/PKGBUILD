pkgname=pulseaudio
pkgver=15.0
pkgrel=5
pkgdesc="A featureful, general-purpose sound server"
arch=('x86_64')
depends=('libsndfile' 'alsa-lib' 'dbus' 'glib2' 'libcap' 'libx11' 'libxcb' 'libice' 'check' 'libsm' 'libxtst')
source=(https://freedesktop.org/software/$pkgname/releases/$pkgname-$pkgver.tar.xz
	pulseaudio.service
	pulseaudio.socket
	pulseaudio-x11.service
	client.conf
	daemon.conf
	default.pa
	system.pa)
install=pulseaudio.install

build() {
	cd $pkgname-$pkgver

	mkdir -p build && cd build

	meson						\
	--prefix=/usr					\
	--buildtype=release				\
	-D database=gdbm				\
	-D systemd=enabled				\
	-D doxygen=true					\
	-D pulsedsp-location='/usr/\$LIB/pulseaudio'	\
	-D stream-restore-clear-old-devices=true	\
	-D udevrulesdir=/usr/lib/udev/rules.d		\
	-D bluez5=disabled				\
	-D elogind=disabled				\
	..

	ninja
}

package() {
	cd $pkgname-$pkgver/build

	DESTDIR=$pkgdir ninja install

	# Superseded by socket activation
	sed -e '/autospawn/iautospawn = no' \
		-i $pkgdir/etc/pulse/client.conf

	# Disable cork-request module, can result in e.g. media players unpausing
	# when there's a Skype call incoming

	sed -e 's|/usr/bin/pactl load-module module-x11-cork-request|#&|' \
		-i $pkgdir/usr/bin/start-pulseaudio-x11

	# Required by qpaeq
	sed -e '/Load several protocols/aload-module module-dbus-protocol' \
		-i $pkgdir/etc/pulse/default.pa

	rm -r $pkgdir/etc/dbus-1

	# Remove systemd services as we run service via xsession at lightdm
	rm -rf $pkgdir/usr/lib/systemd

	# Replcae out stock configs
	cp -vf $srcdir/client.conf $pkgdir/etc/pulse/
	cp -vf $srcdir/daemon.conf $pkgdir/etc/pulse/
	cp -vf $srcdir/default.pa $pkgdir/etc/pulse/
	cp -vf $srcdir/system.pa $pkgdir/etc/pulse/
}
