pkgname=qt5
pkgver=5.15.3
pkgrel=1
arch=('x86_64')
depends=(xorg-server xwayland alsa-lib cups glib2 harfbuzz icu libjpeg libpng
	libtiff libwebp libxkbcommon mesa pcre2 sqlite3 wayland xcb-util-image xcb-util-keysyms
	xcb-util-renderutil xcb-util-wm libinput pciutils pulseaudio bluez libglvnd fontconfig)
makedepends=(extra-cmake-modules)
base_url=https://master.qt.io/archive/qt/5.15
source=(
	# Base src
	$base_url/$pkgver/single/qt-everywhere-opensource-src-5.15.3.tar.xz

	# Additional fixes
	$base_url/CVE-2018-25032-qtbase-5.15.diff
	$base_url/CVE-2022-25643-5.15.diff

	# Final desktop entries
	assistant-qt5.desktop
	designer-qt5.desktop
	linguist-qt5.desktop
	qdbusviewer-qt5.desktop)

prepare() {
	cd qt-everywhere-src-$pkgver

	# cd to qtbase so patches can apply
	cd qtbase

	echo "[ PATCH ]: CVE-2018-25032"
	patch -Np1 -i ../../CVE-2018-25032-qtbase-5.15.diff

	echo "[ PATCH ]: CVE-2022-25643"
	patch -Np1 -i ../../CVE-2022-25643-5.15.diff

	# cd back to main src dir
	cd ../

	# fix some issues using gcc-11
	sed -i '/utility/a #include <limits>'     qtbase/src/corelib/global/qglobal.h
	sed -i '/string/a #include <limits>'      qtbase/src/corelib/global/qfloat16.h
	sed -i '/qbytearray/a #include <limits>'  qtbase/src/corelib/text/qbytearraymatcher.h
	sed -i '/type_traits/a #include <limits>' qtdeclarative/src/qmldebug/qqmlprofilerevent_p.h
}

build() {
	cd qt-everywhere-src-$pkgver

	./configure			\
	-prefix /usr			\
	-headerdir /usr/include/qt	\
	-archdatadir /usr/lib/qt	\
	-datadir /usr/share/qt		\
	-sysconfdir /etc/xdg		\
	-confirm-license		\
	-dbus-linked			\
	-system-harfbuzz		\
	-opensource				\
	-system-sqlite			\
	-nomake examples		\
	-no-rpath			\
	-no-strip			\
	-skip qtwebengine		\
	-journald

	make -j6
}

package() {
	cd qt-everywhere-src-$pkgver

	mkdir -p $pkgdir/usr
	make INSTALL_ROOT=$pkgdir install

	# Remove references to the build directory from installed library dependency (prl)
	find $pkgdir/usr/ -name \*.prl \
		-exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

	# Install images and create the menu entries for installed applications.
	install -v -dm755 $pkgdir/usr/share/pixmaps/
	install -v -Dm644 qttools/src/assistant/assistant/images/assistant-128.png \
		$pkgdir/usr/share/pixmaps/assistant-qt5.png
	install -v -Dm644 qttools/src/designer/src/designer/images/designer.png \
		$pkgdir/usr/share/pixmaps/designer-qt5.png
	install -v -Dm644 qttools/src/linguist/linguist/images/icons/linguist-128-32.png \
		$pkgdir/usr/share/pixmaps/linguist-qt5.png
	install -v -Dm644 qttools/src/qdbus/qdbusviewer/images/qdbusviewer-128.png \
		$pkgdir/usr/share/pixmaps/qdbusviewer-qt5.png
	install -dm755 $pkgdir/usr/share/applications

	cp $srcdir/assistant-qt5.desktop $pkgdir/usr/share/applications/
	cp $srcdir/designer-qt5.desktop $pkgdir/usr/share/applications/
	cp $srcdir/linguist-qt5.desktop $pkgdir/usr/share/applications/
	cp $srcdir/qdbusviewer-qt5.desktop $pkgdir/usr/share/applications/
}
