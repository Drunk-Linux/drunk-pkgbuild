pkgname=lib32-glib2
pkgver=2.70.2
pkgrel=2
pkgdesc="Low level core library (32-bit)"
url="https://wiki.gnome.org/Projects/GLib"
license=(LGPL)
arch=(x86_64)
depends=(lib32-pcre lib32-libffi lib32-util-linux lib32-zlib glib2)
makedepends=(gettext gtk-doc shared-mime-info python3 git util-linux
             meson)
source=("git+https://gitlab.gnome.org/GNOME/glib.git?signed#tag=$pkgver"
        multilib.diff
        gio-querymodules-32.{hook,script})

prepare() {
  cd glib

  # Test suite workaround
  git apply -3 ../multilib.diff
}

build() {
  export CC="gcc -m32 -mstackrealign"
  export CXX="g++ -m32 -mstackrealign"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  CFLAGS+=" -DG_DISABLE_CAST_CHECKS"
  meson glib build \
    --prefix=/usr \
    -D glib_debug=disabled \
    -D selinux=disabled \
    --libdir=/usr/lib32
  meson compile -C build
}

package() {
meson install -C build --destdir "$pkgdir"

  rm -r "$pkgdir"/usr/{share,include}
  find "$pkgdir/usr/bin" -type f -not -name gio-querymodules -printf 'Removing %P\n' -delete
  mv "$pkgdir"/usr/bin/gio-querymodules{,-32}

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook
  install -D gio-querymodules-32.script "$pkgdir/usr/share/libalpm/scripts/gio-querymodules-32"
}
