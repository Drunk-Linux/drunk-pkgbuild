# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>
# Contributor: J0k3r <moebius282@gmail.com>

pkgname=lib32-sdl2
pkgver=2.0.20
pkgrel=1
arch=(x86_64)
depends=(
    lib32-libxext lib32-libxrender lib32-libx11 lib32-libglvnd
    lib32-libxcursor lib32-alsa-lib lib32-pulseaudio
    lib32-libxinerama lib32-libxkbcommon lib32-libxrandr lib32-libxss
    lib32-mesa lib32-wayland sdl2
)
makedepends=(
    cmake git jack2 ninja wayland-protocols
)
_tag=b424665e0899769b200231ba943353a5fee1b6b6
source=(git+https://github.com/libsdl-org/SDL.git#tag=${_tag})

pkgver() {
  cd SDL
  git describe --tags | sed 's/^release-//'
}

build() {
    export CC='gcc -m32'
    export CXX='g++ -m32'
    export CFLAGS+=" -ffat-lto-objects"
    export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

    cmake -S SDL -B build -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib32 \
    -DSDL_DLOPEN=ON \
    -DSDL_RPATH=OFF \
    -DSDL_STATIC=OFF

    cmake --build build
}

package() {
    DESTDIR="${pkgdir}" cmake --install build
    rm -rf "${pkgdir}"/usr/{bin,include,share}
    sed -i "s/libSDL2\.a/libSDL2main.a/g" "$pkgdir"/usr/lib32/cmake/SDL2/SDL2Targets-noconfig.cmake
}
