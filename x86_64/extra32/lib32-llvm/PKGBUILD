pkgname=(lib32-llvm)
pkgver=13.0.0
pkgrel=1
arch=('x86_64')
url="https://llvm.org/"
license=('custom:Apache 2.0 with LLVM Exception')
makedepends=('cmake' 'ninja' 'lib32-libffi' 'lib32-zlib' 'python3'
             'lib32-libxml2')
options=('staticlibs' '!lto') # extra/llvm has many test failures with LTO
_source_base=https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver
source=($_source_base/llvm-$pkgver.src.tar.xz{,.sig}
        no-strict-aliasing-DwarfCompileUnit.patch)

prepare() {
	cd "$srcdir/llvm-$pkgver.src"
	mkdir build

	# https://bugs.llvm.org/show_bug.cgi?id=50611#c3
	patch -Np2 -i ../no-strict-aliasing-DwarfCompileUnit.patch
}

build() {
	cd "$srcdir/llvm-$pkgver.src/build"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	cmake .. -G Ninja \
	-DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DLLVM_LIBDIR_SUFFIX=32 \
	-DCMAKE_C_FLAGS:STRING=-m32 \
	-DCMAKE_CXX_FLAGS:STRING=-m32 \
	-DLLVM_TARGET_ARCH:STRING=i686 \
	-DLLVM_HOST_TRIPLE=$CHOST \
	-DLLVM_DEFAULT_TARGETS_TRIPLE="i686-pc-linux-gnu" \
	-DLLVM_BUILD_LLVM_DYLIB=ON \
	-DLLVM_LINK_LLVM_DYLIB=ON \
	-DLLVM_ENABLE_RTTI=ON \
	-DLLVM_ENABLE_FFI=ON \
	-DLLVM_BUILD_DOCS=OFF \
	-DLLVM_ENABLE_SPHINX=OFF \
	-DLLVM_ENABLE_DOXYGEN=OFF \
	-DLLVM_BINUTILS_INCDIR=/usr/include

	ninja all
}

package() {
	cd "$srcdir/llvm-$pkgver.src/build"

	DESTDIR="$pkgdir" ninja install

	# Fix permissions of static libs
	chmod -x "$pkgdir"/usr/lib32/*.a

	mv "$pkgdir/usr/bin/llvm-config" "$pkgdir/usr/lib32/llvm-config"
	mv "$pkgdir/usr/include/llvm/Config/llvm-config.h" \
		"$pkgdir/usr/lib32/llvm-config-32.h"

	rm -rf "$pkgdir"/usr/{bin,include,share/{doc,man,llvm,opt-viewer}}

	# Header stub is taken from Fedora
	install -d "$pkgdir/usr/include/llvm/Config"
	mv "$pkgdir/usr/lib32/llvm-config-32.h" "$pkgdir/usr/include/llvm/Config/"

	mkdir "$pkgdir"/usr/bin
	mv "$pkgdir/usr/lib32/llvm-config" "$pkgdir/usr/bin/llvm-config32"

	# Symlink LLVMgold.so from /usr/lib/bfd-plugins
	install -d "$pkgdir/usr/lib32/bfd-plugins"
	ln -s ../LLVMgold.so "$pkgdir/usr/lib32/bfd-plugins/LLVMgold.so"
}
